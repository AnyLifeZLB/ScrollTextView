// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.1'


        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'com.novoda:bintray-release:0.3.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


ext {
    userOrg = 'qinglinyi'
    groupId = 'com.qinglinyi.arg'
    description = 'fragment arg'
    publishVersion = '0.9.2'
    website = 'https://github.com/qinglinyi/FragmentArg'
    git_url = 'https://github.com/qinglinyi/FragmentArg.git'
    dryRun = 'false'
    IS_UPLOADING = project.getGradle().startParameter.taskNames.any { it.contains('bintrayUpload') }
}


subprojects {
    repositories {
        jcenter()
    }

    group = groupId
    version = publishVersion
    if (IS_UPLOADING && project.name in['compiler','api']) {

        println project.name
        apply plugin: 'maven'

        gradle.taskGraph.whenReady { taskGraph ->
            def pomTask = taskGraph.getAllTasks().find {
                it.path == ":$project.name:generatePomFileForMavenPublication"
            }
            if (pomTask == null) return;
            pomTask.doLast {
                file("build/publications/maven/pom-default.xml").delete()
                pom {
                    //noinspection GroovyAssignabilityCheck
                    project {
                        name "arg-$project.name"
                        artifactId "arg-$project.name"
                        packaging 'jar'
                        description 'An annotation processor Fragment Argument.'
                        url git_url
                        version publishVersion

                        scm {
                            url git_url
                            connection git_url
                            developerConnection git_url
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                            }
                        }

                        developers {
                            developer {
                                id 'anylifezlb'
                                name '曾清远'
                                email 'anylife.zlb@gmail.com'
                            }
                        }
                    }
                }.writeTo("build/publications/maven/pom-default.xml")
            }
        }
    }
}